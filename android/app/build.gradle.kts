import java.io.FileInputStream
import java.util.Properties
import java.util.Base64

plugins {
    id("com.android.application")
    id("kotlin-android")
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id("dev.flutter.flutter-gradle-plugin")
}


// Function to decode and parse the dart-defines
fun getDartDefines(): Map<String, String> {
    val dartDefines = mutableMapOf<String, String>()
    if (project.hasProperty("dart-defines")) {
        // The property "dart-defines" is a comma-separated, base64-encoded string
        project.property("dart-defines").toString().split(',').forEach { encodedDefine ->
            val decoded = String(Base64.getDecoder().decode(encodedDefine), Charsets.UTF_8)
            val parts = decoded.split('=', limit = 2)
            if (parts.size == 2) {
                dartDefines[parts[0]] = parts[1]
            }
        }
    }
    return dartDefines
}

val keystoreProperties = Properties()
val keystorePropertiesFile = rootProject.file("key.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(FileInputStream(keystorePropertiesFile))
}

android {
    namespace = "mx.com.gnp.medicos"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId = "mx.com.gnp.medicos"
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName

        val defines = getDartDefines()
        manifestPlaceholders["androidFirebaseApiKey"] = defines["androidFirebaseApiKey"] ?: ""
    }

    signingConfigs {
        create("release") {
            keyAlias = keystoreProperties["keyAlias"] as String?
            keyPassword = keystoreProperties["keyPassword"] as String?
            storeFile = keystoreProperties["storeFile"]?.let { rootProject.file(it as String) }
            storePassword = keystoreProperties["storePassword"] as String?
        }
    }

    buildTypes {
        release {
            // El signingConfig se asigna dinámicamente abajo en productFlavors.all
        }
    }

    // Configuración dinámica de firmas por Flavor
    productFlavors.all {
        signingConfig = if (name == "prod") {
            signingConfigs.getByName("release")
        } else {
            signingConfigs.getByName("debug")
        }
    }
}

flutter {
    source = "../.."
}
// ----- BEGIN flavorDimensions (autogenerated by flutter_flavorizr) -----
apply { from("flavorizr.gradle.kts") }
// ----- END flavorDimensions (autogenerated by flutter_flavorizr) -----
